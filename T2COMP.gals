#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = SLR
#RegularDefinitions
D : [0-9]
#Tokens
VAR : [a-z A-Z][a-z A-Z 0-9 _]*

INICIO = VAR : "inicio"
FIM   = VAR : "fim"
SE = VAR : "se"
SENAO = VAR : "senao"
LEIA = VAR : "leia"
ESCREVA = VAR : "escreva"
FUNCTION = VAR : "funcao"
RETORNO = VAR : "retorno"
ENQUANTO = VAR : "enquanto"
PARA = VAR : "para"
FACA = VAR : "faca"
TINT = VAR : "int"
TFLOAT = VAR : "float"
TBIN = VAR : "bin"
THEX = VAR : "hex"
TSTRING = VAR : "string"
COMENTARIO: [/][/][^\n\r]*
MCOMENTARIO: /\*([^\*]|\*+[^/\*])*\*+/

"%"	
"+"
"-"
"*"
"/"
"("
")"
"=="
"!="
"!"
">="
"<="
"="
"&&"
"||"
">>"
"<<"
"<"
">"
"&"
"|"
"~"
"^"
";"
","
"."
":"
"["
"]"
"{"
"}"

HEXADECIMAL: 0x[0-9a-fA-F]+
BINARIO : 0b[0-1]+
INTEIRO : {D}+
REAL: {D}+\.{D}*
STRING: \"[^\n\r\"]*\"



 : [\s\t\n\r]*
#NonTerminals
<init>
<l_cmd>
<cmd>
<var>
<tipo>
<atr>
<cmdse>
<cmdenquanto>
<comentario>
<paravar>
<inout>
<escrevavar>
<function>
<parametros>
<parametrosvar>
<retorno>
<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<op_rel>
<op_neg>
<op_bitwise>
<op_arit_baixa>
<op_arit_alta>
<op_arit_mais_alta>
#Grammar
<init> ::= INICIO <l_cmd> FIM#11;
<l_cmd> ::= <l_cmd> <cmd>| <cmd>;
<cmd> ::= <var> ";"| <exp> ";" | <comentario> | <cmdse> | <cmdenquanto> | <inout>";" | <function> | <atr>";" |<retorno> ";";
<var> ::= <tipo> VAR#2#21 | <tipo> VAR#7#22 "[" INTEIRO#8#23 "]" | <tipo> VAR#12#24 "=" <exp>#28 ;
<tipo> ::= TINT #1| TFLOAT #1| THEX #1| TBIN #1| TSTRING #1;
<atr> ::= VAR#13#25 "=" <exp> #28| VAR#14#26 "[" INTEIRO#15#27 "]" "=" <exp> #28;
<cmdse> ::= SE"(" <exp> ")" "{"#38 <l_cmd> "}"#39 | SE"(" <exp> ")" "{"#38 <l_cmd> "}"SENAO "{"#40 <l_cmd> "}"#39 ;
<cmdenquanto> ::= ENQUANTO "("#41 <exp> ")" "{"#42 <l_cmd> "}"#43 | FACA "{"#41 <l_cmd> "}" ENQUANTO "(" <exp> ")"#42 ";" | PARA "("<paravar> ";"#41 <exp> ";" #44<atr>#45 ")" "{"#42 <l_cmd> #46"}"#43;
<paravar> ::= VAR | <atr> | <var>;
<inout> ::= LEIA "(" VAR#9#34 ")" | ESCREVA "("<escrevavar>")";
<escrevavar> ::= VAR#10#35 | STRING#35;
<comentario> ::= COMENTARIO | MCOMENTARIO;
<function> ::= FUNCTION VAR#3 "("<parametros>")"#5 "{" <l_cmd> "}"#6;
<parametros> ::= <parametros>"," <parametrosvar> | <parametrosvar>;
<parametrosvar> ::= <paravar> | <exp> | <atr>;
<retorno> ::= RETORNO <paravar> | RETORNO <exp>;
<exp>  ::= <exp> "||" <exp1> | <exp1>;
<exp1> ::= <exp1> "&&" <exp2> | <exp2>;
<exp2> ::= <exp2> "|"#29 <exp3> | <exp3>;
<exp3> ::= <exp3> "^" <exp4> | <exp4>;
<exp4> ::= <exp4> "&"#29 <exp5> | <exp5>;
<exp5> ::= <exp5> <op_rel> <exp6> #37 | <exp6>;
<exp6> ::= <exp6> <op_bitwise> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arit_baixa> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_arit_alta> <exp9> | <exp9> ;
<exp9> ::= <op_neg> <exp10> | <exp10>;
<exp10> ::= INTEIRO #30 | REAL #30 | VAR#4#31 | "(" <exp> ")" | VAR "("<parametros>")" | VAR#32 "[" INTEIRO#33 "]";
<op_rel> ::= ">" #36 | "<" #36 | ">=" #36 | "<=" #36 | "!=" #36 | "==" #36; 
<op_neg> ::= "-" | "~" | "!";
<op_bitwise> ::= "<<"#29 | ">>"#29;
<op_arit_baixa> ::= "+" #29 | "-" #29 ;
<op_arit_alta> ::= "*" #29 | "/" #29 | "%" #29;
